     1                                  ; =================================================================
     2                                  ; DENSAN V53 VME board "RAM TEST" test ROM
     3                                  ; =================================================================
     4                                  
     5                                  ; ▼▼▼ ROMサイズ設定 (ここを環境に合わせる) ▼▼▼
     6                                  %define ROM_SIZE_1024  0x20000  ; 27C010 (128KB)
     7                                  ; 使用するROMサイズを選択
     8                                  %define ROM_TOTAL     ROM_SIZE_1024
     9                                  ; ▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲
    10                                  
    11                                  ; ==========================================
    12                                  ; V53 System Register
    13                                  ; ==========================================
    14                                  %define SCTL    0x0FFFE ; システム・コントロール・レジスタ
    15                                  %define OPSEL   0x0FFFD ; 内蔵ペリフェラル選択レジスタ
    16                                  %define OPHA    0x0FFFC ; 内蔵ペリフェラル・リロケーション・レジスタ
    17                                  %define DULA    0x0FFFB ; 
    18                                  %define IULA    0x0FFFA ; 
    19                                  %define TULA    0x0FFF9 ; 
    20                                  %define SULA    0x0FFF8 ; SCUリロケーション・レジスタ 
    21                                  %define WCY4    0x0FFF6 ; 
    22                                  %define WCY3    0x0FFF5 ; 
    23                                  %define WCY2    0x0FFF4 ; 
    24                                  %define WMB1    0x0FFF3 ; 
    25                                  %define RFC     0x0FFF2 ; 
    26                                  %define SBCR    0x0FFF1 ; 
    27                                  %define TCKS    0x0FFF0 ; 
    28                                  %define WAC     0x0FFED ; 
    29                                  %define WCY0    0x0FFEC ; 
    30                                  %define WCY1    0x0FFEB ; 
    31                                  %define WMB0    0x0FFEA ; 
    32                                  %define BRC     0x0FFE9 ; ボー・レート・カウンタ
    33                                  %define BADR    0x0FFE1 ; 
    34                                  %define BSEL    0x0FFE0 ; 
    35                                  %define XAM     0x0FF80 ; 
    36                                  %define PGR     0x0FF00 ; 
    37                                  
    38                                  ; SCUレジスタ (SCUは1260Hに配置）
    39                                  %define SCU_DATA 0x01260 ; 送受データ・レジスタ(R:SRB/W:STB)
    40                                  %define SCU_SST  0x01261 ; ステータス・レジスタ(R:SST)
    41                                  %define SCU_SCM  0x01261 ; コマンドレジスタ(W:SCM)
    42                                  %define SCU_SMD  0x01262 ; シリアルモード設定(W:SMD)
    43                                  %define SCU_SIMK 0x01263 ; シリアル割り込みマスクレジスタ(R/W:SIMK)
    44                                  
    45                                  cpu 186
    46                                  bits 16
    47                                  
    48                                  ; ==========================================
    49                                  ; コードセクション (ROMの先頭付近)
    50                                  ; ==========================================
    51                                  section .text
    52                                  org 0
    53                                  
    54                                  ; -----------------------------------------------------------------
    55                                  ; Entry Point (Offset 0x0000)
    56                                  ; -----------------------------------------------------------------
    57                                  start:
    58 00000000 FA                          cli             ; 割り込み禁止
    59                                  
    60                                      ; ---------------------------------------------
    61                                      ; 1. セグメントレジスタ初期化
    62                                      ; ※CSはJMP命令で設定されるので、DS/ES/SSを設定する
    63                                      ; ---------------------------------------------
    64 00000001 B80000                      mov ax, 0x0000
    65 00000004 8ED8                        mov ds, ax      ; DS (Data Segment) を0000Hに設定し、ここをテスト対象とする
    66 00000006 8EC0                        mov es, ax
    67                                      ;mov ss, ax     ; スタックは使わない
    68                                      ;mov sp, 0
    69                                      
    70                                      ; ---------------------------------------------
    71                                      ; 2. システム・コントロール・レジスタ (SCTL) の設定
    72                                      ; ビット4 (SC) = 1 : 内蔵BRGを使用
    73                                      ; ビット0 (IOAG) = 1 : 8ビット・バウンダリ（連続配置）
    74                                      ; ---------------------------------------------
    75 00000008 BAFEFF                      mov dx, SCTL
    76 0000000B B011                        mov al, 00010001b
    77 0000000D EE                          out dx, al
    78                                  
    79                                      ; ---------------------------------------------
    80                                      ; 3. ボーレートジェネレータ (BRG) の設定
    81                                      ;  38400bps設定 (fx=16MHz, factor=x16)
    82                                      ;  16,000,000/(16×38400) ≈ 26
    83                                      ; ---------------------------------------------
    84 0000000E BAE9FF                      mov dx, BRC
    85 00000011 B01A                        mov al, 26
    86 00000013 EE                          out dx, al
    87                                  
    88                                      ; ---------------------------------------------
    89                                      ; 4. IOアドレスの設定 (0x1260にSCUを配置)
    90                                      ; OPHA (FFFCH) に 12H を設定 (ベースアドレス上位8ビット)
    91                                      ; SULA (FFF8H) に 60H を設定 (SCUのオフセット)
    92                                      ; ---------------------------------------------
    93 00000014 BAFCFF                      mov dx, OPHA
    94 00000017 B012                        mov al, 0x12    ; 上位アドレス
    95 00000019 EE                          out dx, al
    96                                  
    97 0000001A BAF8FF                      mov dx, SULA
    98 0000001D B060                        mov al, 0x60    ; 下位アドレス
    99 0000001F EE                          out dx, al
   100                                      
   101                                      ; ---------------------------------------------
   102                                      ; 5. 内蔵周辺選択レジスタ (OPSEL) でSCUを有効化
   103                                      ; ビット3 (SS) = 1 : SCUの使用を許可
   104                                      ; ---------------------------------------------
   105 00000020 BAFDFF                      mov dx, OPSEL
   106 00000023 B008                        mov al, 00001000b   ; Bit3 (SS): SCUの使用を許可
   107 00000025 EE                          out dx, al
   108                                  
   109                                      ; ---------------------------------------------
   110                                      ; 6. SCU内部レジスタの初期化 (配置した1260Hを使用)
   111                                      ; ---------------------------------------------
   112                                      ; 動作モード設定 SMDレジスタ
   113                                      ; Mode: 非同期, 8bit, パリティなし, 1ストップビット, x16クロック
   114                                      ; 01 00 11 10
   115 00000026 BA6212                      mov dx, SCU_SMD
   116 00000029 B04E                        mov al, 01001110b
   117 0000002B EE                          out dx, al
   118                                  
   119                                      ; コマンド設定  SCMレジスタ
   120                                      ; Cmd: 送受信イネーブル, エラーリセット, DTR/RTSアクティブ
   121                                      ; 00 0 1 0 1 0 1
   122 0000002C BA6112                      mov dx, SCU_SCM
   123 0000002F B015                        mov al, 00010101b   ; RTS/DTR ON, RX/TX Enabl
   124 00000031 EE                          out dx, al
   125                                  
   126                                      ; 割り込みはマスクする  SIMKレジスタ
   127 00000032 BA6312                      mov dx, SCU_SIMK
   128 00000035 B003                        mov al, 00000011b
   129 00000037 EE                          out dx, al
   130                                  
   131                                      ; ---------------------------------------------
   132                                      ; 7. メモリ書き込み後に読み出してメモリテストをするループ
   133                                      ; ---------------------------------------------
   134                                      ; --- Stackless Main Loop ---
   135                                      ; CALL も PUSH も使わず、ひたすらベタ書きで回す
   136                                  
   137                                      ; ループカウンタ兼アドレスポインタ (SIを使用)
   138                                      ; SI = 0000H からスタート
   139 00000038 BE0000                      mov si, 0x0000
   140                                  
   141                                  main_loop:
   142                                      ; --- 書き込み ---
   143                                      ; アドレス [DS:SI] にテスト値を書き込む
   144 0000003B C60455                      mov byte [si], 0x55     ; 書き込む値(01010101)
   145                                  
   146                                      ; --- 読み出し ---
   147                                      ; アドレス [DS:SI] から値を読み出す
   148 0000003E 8A04                        mov al, byte [si]
   149                                  
   150                                      ; --- 比較 ---
   151 00000040 3C55                        cmp al, 0x55
   152 00000042 7532                        jne error_found_55         ; 一致しない場合はエラー処理へ
   153                                  
   154                                      ; 同様に0xAA(10101010)でも確認
   155 00000044 C604AA                      mov byte [si], 0xAA
   156 00000047 8A04                        mov al, byte [si]
   157 00000049 3CAA                        cmp al, 0xAA
   158 0000004B 752D                        jne error_found_AA
   159                                  
   160                                      ; メモリをクリアして次へ
   161 0000004D C60400                      mov byte [si], 0x00
   162                                  
   163                                  next_addr:
   164                                      ; --- アドレス更新 ---
   165 00000050 46                          inc si                 ; アドレスをインクリメント
   166 00000051 83FE00                      cmp si, 0000H           ; 0000Hに戻ったかチェック (FFFFHの次は0000H)
   167 00000054 7402                        je  segment_update      ; SIが0になったら次のセグメントへ
   168 00000056 EBE3                        jmp main_loop           ; 0でなければループ継続
   169                                  
   170                                  segment_update:
   171                                      ; --- セグメント更新 ---
   172 00000058 8CD8                        mov ax, ds
   173 0000005A 050010                      add ax, 1000h
   174 0000005D 8ED8                        mov ds, ax
   175                                  
   176                                      ; --- 終了判定（E0000-FFFFFはROMエリアのため省く）---
   177 0000005F 3D00E0                      cmp ax, 0e000h
   178 00000062 7402                        je all_done
   179                                  
   180 00000064 EBD5                        jmp main_loop
   181                                  
   182                                  all_done:
   183                                      ; --- 終了処理 (全チェック完了) ---
   184                                      ; シリアル送信部
   185 00000066 BA6112                      mov dx, SCU_SST
   186                                  .wait_tx3:
   187 00000069 EC                          in al, dx
   188 0000006A A801                        test al, 00000001b  ; TX Ready?
   189 0000006C 74FB                        jz .wait_tx3
   190                                      
   191 0000006E BA6012                      mov dx, SCU_DATA
   192 00000071 B023                        mov al, '#'         ; 終了したことを示す表示
   193 00000073 EE                          out dx, al
   194                                  
   195                                      ;無限ループで停止
   196 00000074 EBFE                        jmp $
   197                                  ; -------------------------------------------------------------
   198                                  ; エラー分岐用フック
   199                                  ; -------------------------------------------------------------
   200                                  error_found_55:
   201                                      ; ALには読み出した「間違った値」が入っている
   202 00000076 88C7                        mov bh, al          ; BHに読み出し値を保存 (BLは文字出力で使うのでBHを使う)
   203 00000078 EB04                        jmp error_found_main
   204                                  
   205                                  error_found_AA:
   206 0000007A 88C7                        mov bh, al          ; BHに読み出し値を保存
   207 0000007C EB00                        jmp error_found_main
   208                                  
   209                                  ; -------------------------------------------------------------
   210                                  ; エラー表示ルーチン
   211                                  ; 出力形式: SSSS OOOO VV (Segment Offset Value)
   212                                  ; -------------------------------------------------------------
   213                                  error_found_main:
   214                                      ; 状態フラグ(BX)を使って、セグメント表示とオフセット表示で
   215                                      ; 同じ処理(PRINT_HEX)を使い回す。
   216                                      ; レジスタ競合回避のため、DIをフラグとして使用
   217                                      ; DI = 2 : セグメント表示 (4桁)
   218                                      ; DI = 1 : オフセット表示 (4桁)
   219                                      ; DI = 0 : 読み出し値表示 (2桁)
   220                                  
   221 0000007E BF0200                      mov di, 2   
   222                                  
   223                                  setup_print_phase:
   224                                      ; フェーズに応じた準備
   225 00000081 83FF02                      cmp di, 2       ; セグメント(DS)を表示
   226 00000084 7407                        je phase_segment
   227 00000086 83FF01                      cmp di, 1
   228 00000089 7409                        je phase_offset
   229 0000008B EB0E                        jmp phase_value
   230                                  
   231                                  phase_segment:
   232 0000008D 8CDD                        mov bp, ds              ; 表示データ: DS
   233 0000008F B90400                      mov cx, 4               ; 4桁分ループ (4 nibbles)
   234 00000092 EB12                        jmp print_hex_start
   235                                  
   236                                  phase_offset:
   237 00000094 89F5                        mov bp, si          ; 表示データ: SI
   238 00000096 B90400                      mov cx, 4           ; 4桁
   239 00000099 EB0B                        jmp print_hex_start
   240                                  
   241                                  phase_value:
   242                                      ; BHに入っている8bit値を表示
   243 0000009B 88F8                        mov al, bh
   244 0000009D B400                        mov ah, 0           ; AX = 00xxh
   245 0000009F 89C5                        mov bp, ax          ; 表示データ: AX
   246 000000A1 B90200                      mov cx, 2           ; 8bitなので2桁でOK
   247 000000A4 EB00                        jmp print_hex_start
   248                                  
   249                                  print_hex_start:
   250                                      ; --- 16進数出力ループ ---
   251                                  print_hex_loop:
   252                                      ; 上位4ビットを取り出すために左ローテート
   253 000000A6 D1C5                        rol bp, 1
   254 000000A8 D1C5                        rol bp, 1
   255 000000AA D1C5                        rol bp, 1
   256 000000AC D1C5                        rol bp, 1
   257                                      
   258 000000AE 89E8                        mov ax, bp              ; AXにコピー
   259 000000B0 83E00F                      and ax, 000fh           ; 下位4ビットのみ抽出 (0-F)
   260                                  
   261                                      ; HEX ASCII変換
   262 000000B3 3C0A                        cmp al, 0ah
   263 000000B5 7202                        jb  is_digit
   264 000000B7 0407                        add al, 07h             ; 'A'-'F' の場合の調整
   265                                  is_digit:
   266 000000B9 0430                        add al, 30h             ; '0' のASCIIコードを足す
   267                                  
   268                                      ; --- 文字送信 (SCU) ---
   269                                      ; レジスタ退避ができないため、他のレジスタを壊さないよう注意
   270                                      ; ここでは AH を作業用、AL を送信データとして使用
   271                                      
   272 000000BB 88C3                        mov bl, al              ; 送信文字をBLに一時退避
   273                                  
   274                                      ; シリアル送信部
   275 000000BD BA6112                      mov dx, SCU_SST
   276                                  .wait_tx:
   277 000000C0 EC                          in al, dx
   278 000000C1 A801                        test al, 00000001b  ; TX Ready?
   279 000000C3 74FB                        jz .wait_tx
   280                                      
   281 000000C5 BA6012                      mov dx, SCU_DATA
   282 000000C8 88D8                        mov al, bl
   283 000000CA EE                          out dx, al
   284                                  
   285 000000CB E2D9                        loop print_hex_loop     ; CXを減らしてループ
   286                                  
   287                                      ; --- 16進数表示完了後の処理 ---
   288                                      ; 読みやすくするため16進数の後にスペースをいれる
   289                                      ; シリアル送信部
   290 000000CD BA6112                      mov dx, SCU_SST
   291                                  .wait_tx2:
   292 000000D0 EC                          in al, dx
   293 000000D1 A801                        test al, 00000001b  ; TX Ready?
   294 000000D3 74FB                        jz .wait_tx2
   295                                      
   296 000000D5 BA6012                      mov dx, SCU_DATA
   297 000000D8 B020                        mov al, ' '
   298 000000DA EE                          out dx, al
   299                                  
   300                                      ; --- 次のフェーズへ遷移 ---
   301 000000DB 4F                          dec di              ; 2->1->0->(-1)
   302 000000DC 83FFFF                      cmp di, -1
   303 000000DF 7402                        je error_done       ; 0(値)まで表示し終わったら完了
   304                                  
   305 000000E1 EB9E                        jmp setup_print_phase
   306                                  
   307                                  error_done:
   308                                      ; --- テスト継続 ---
   309 000000E3 E96AFF                      jmp next_addr
   310                                  
   311                                  ; -----------------------------------------------------------------
   312                                  ; Padding (空白埋め)
   313                                  ; コードの終わりから、リセットベクタの手前までを0xFFで埋める
   314                                  ; -----------------------------------------------------------------
   315 000000E6 FF<rep 1FF0Ah>              times ROM_TOTAL - 16 - ($ - $$) db 0xFF
   316                                  
   317                                  ; -----------------------------------------------------------------
   318                                  ; Reset Vector (Physical Address FFFF0h)
   319                                  ; CPUは電源ON時、ここ(ROMの末尾16バイト地点)を実行する
   320                                  ; -----------------------------------------------------------------
   321                                  reset_vector:
   322                                      ; ROMの先頭(start)へジャンプする
   323                                      ; 1MBit ROMの場合、物理アドレスは E0000h～FFFFFh にマップされる想定
   324                                      ; なので、セグメント E000h, オフセット 0000h へ飛べばよい
   325                                      
   326 0001FFF0 EA000000E0                  jmp 0xE000:0000   ; Far Jump to start of 128KB ROM
   327                                  
   328                                      ; ファイルサイズがぴったりROMサイズになるよう調整
   329 0001FFF5 FF<rep Bh>                  times 16 - ($ - reset_vector) db 0xFF
