     1                                  ; =================================================================
     2                                  ; DENSAN V53 VME board "RAM TEST" test ROM
     3                                  ; =================================================================
     4                                  
     5                                  ; ▼▼▼ ROMサイズ設定 (ここを環境に合わせる) ▼▼▼
     6                                  %define ROM_SIZE_1024  0x20000  ; 27C010 (128KB)
     7                                  ; 使用するROMサイズを選択
     8                                  %define ROM_TOTAL     ROM_SIZE_1024
     9                                  ; ▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲▲
    10                                  
    11                                  ; ==========================================
    12                                  ; V53 System Register
    13                                  ; ==========================================
    14                                  %define SCTL    0x0FFFE ; システム・コントロール・レジスタ
    15                                  %define OPSEL   0x0FFFD ; 内蔵ペリフェラル選択レジスタ
    16                                  %define OPHA    0x0FFFC ; 内蔵ペリフェラル・リロケーション・レジスタ
    17                                  %define DULA    0x0FFFB ; 
    18                                  %define IULA    0x0FFFA ; 
    19                                  %define TULA    0x0FFF9 ; 
    20                                  %define SULA    0x0FFF8 ; SCUリロケーション・レジスタ 
    21                                  %define WCY4    0x0FFF6 ; 
    22                                  %define WCY3    0x0FFF5 ; 
    23                                  %define WCY2    0x0FFF4 ; 
    24                                  %define WMB1    0x0FFF3 ; 
    25                                  %define RFC     0x0FFF2 ; 
    26                                  %define SBCR    0x0FFF1 ; 
    27                                  %define TCKS    0x0FFF0 ; 
    28                                  %define WAC     0x0FFED ; 
    29                                  %define WCY0    0x0FFEC ; 
    30                                  %define WCY1    0x0FFEB ; 
    31                                  %define WMB0    0x0FFEA ; 
    32                                  %define BRC     0x0FFE9 ; ボー・レート・カウンタ
    33                                  %define BADR    0x0FFE1 ; 
    34                                  %define BSEL    0x0FFE0 ; 
    35                                  %define XAM     0x0FF80 ; 
    36                                  %define PGR     0x0FF00 ; 
    37                                  
    38                                  ; SCUレジスタ (SCUは1260Hに配置）
    39                                  %define SCU_DATA 0x01260 ; 送受データ・レジスタ(R:SRB/W:STB)
    40                                  %define SCU_SST  0x01261 ; ステータス・レジスタ(R:SST)
    41                                  %define SCU_SCM  0x01261 ; コマンドレジスタ(W:SCM)
    42                                  %define SCU_SMD  0x01262 ; シリアルモード設定(W:SMD)
    43                                  %define SCU_SIMK 0x01263 ; シリアル割り込みマスクレジスタ(R/W:SIMK)
    44                                  
    45                                  cpu 186
    46                                  bits 16
    47                                  
    48                                  ; ==========================================
    49                                  ; コードセクション (ROMの先頭付近)
    50                                  ; ==========================================
    51                                  section .text
    52                                  org 0
    53                                  
    54                                  ; -----------------------------------------------------------------
    55                                  ; Entry Point (Offset 0x0000)
    56                                  ; -----------------------------------------------------------------
    57                                  start:
    58 00000000 FA                          cli             ; 割り込み禁止
    59                                  
    60                                      ; ---------------------------------------------
    61                                      ; 1. セグメントレジスタ初期化
    62                                      ; ※CSはJMP命令で設定されるので、DS/ES/SSを設定する
    63                                      ; ---------------------------------------------
    64 00000001 B80000                      mov ax, 0x0000
    65 00000004 8ED8                        mov ds, ax      ; DS (Data Segment) を0000Hに設定し、ここをテスト対象とする
    66 00000006 8EC0                        mov es, ax
    67                                      ;mov ss, ax     ; スタックは使わない
    68                                      ;mov sp, 0
    69                                      
    70                                      ; ---------------------------------------------
    71                                      ; 2. システム・コントロール・レジスタ (SCTL) の設定
    72                                      ; ビット4 (SC) = 1 : 内蔵BRGを使用
    73                                      ; ビット0 (IOAG) = 1 : 8ビット・バウンダリ（連続配置）
    74                                      ; ---------------------------------------------
    75 00000008 BAFEFF                      mov dx, SCTL
    76 0000000B B011                        mov al, 00010001b
    77 0000000D EE                          out dx, al
    78                                  
    79                                      ; ---------------------------------------------
    80                                      ; 3. ボーレートジェネレータ (BRG) の設定
    81                                      ;  38400bps設定 (fx=16MHz, factor=x16)
    82                                      ;  16,000,000/(16×38400) ≈ 26
    83                                      ; ---------------------------------------------
    84 0000000E BAE9FF                      mov dx, BRC
    85 00000011 B01A                        mov al, 26
    86 00000013 EE                          out dx, al
    87                                  
    88                                      ; ---------------------------------------------
    89                                      ; 4. IOアドレスの設定 (0x1260にSCUを配置)
    90                                      ; OPHA (FFFCH) に 12H を設定 (ベースアドレス上位8ビット)
    91                                      ; SULA (FFF8H) に 60H を設定 (SCUのオフセット)
    92                                      ; ---------------------------------------------
    93 00000014 BAFCFF                      mov dx, OPHA
    94 00000017 B012                        mov al, 0x12    ; 上位アドレス
    95 00000019 EE                          out dx, al
    96                                  
    97 0000001A BAF8FF                      mov dx, SULA
    98 0000001D B060                        mov al, 0x60    ; 下位アドレス
    99 0000001F EE                          out dx, al
   100                                      
   101                                      ; ---------------------------------------------
   102                                      ; 5. 内蔵周辺選択レジスタ (OPSEL) でSCUを有効化
   103                                      ; ビット3 (SS) = 1 : SCUの使用を許可
   104                                      ; ---------------------------------------------
   105 00000020 BAFDFF                      mov dx, OPSEL
   106 00000023 B008                        mov al, 00001000b   ; Bit3 (SS): SCUの使用を許可
   107 00000025 EE                          out dx, al
   108                                  
   109                                      ; ---------------------------------------------
   110                                      ; 6. SCU内部レジスタの初期化 (配置した1260Hを使用)
   111                                      ; ---------------------------------------------
   112                                      ; 動作モード設定 SMDレジスタ
   113                                      ; Mode: 非同期, 8bit, パリティなし, 1ストップビット, x16クロック
   114                                      ; 01 00 11 10
   115 00000026 BA6212                      mov dx, SCU_SMD
   116 00000029 B04E                        mov al, 01001110b
   117 0000002B EE                          out dx, al
   118                                  
   119                                      ; コマンド設定  SCMレジスタ
   120                                      ; Cmd: 送受信イネーブル, エラーリセット, DTR/RTSアクティブ
   121                                      ; 00 0 1 0 1 0 1
   122 0000002C BA6112                      mov dx, SCU_SCM
   123 0000002F B015                        mov al, 00010101b   ; RTS/DTR ON, RX/TX Enabl
   124 00000031 EE                          out dx, al
   125                                  
   126                                      ; 割り込みはマスクする  SIMKレジスタ
   127 00000032 BA6312                      mov dx, SCU_SIMK
   128 00000035 B003                        mov al, 00000011b
   129 00000037 EE                          out dx, al
   130                                  
   131                                  
   132                                      ; ---------------------------------------------
   133                                      ; 7. 受信したデータをそのまま送信するループ
   134                                      ; ---------------------------------------------
   135                                      ; --- Stackless Main Loop ---
   136                                      ; CALL も PUSH も使わず、ひたすらベタ書きで回す
   137                                  
   138                                      ; ループカウンタ兼アドレスポインタ (SIを使用)
   139                                      ; SI = 0000H からスタート
   140 00000038 BE0000                      mov si, 0x0000
   141                                  
   142                                  main_loop:
   143                                      ; --- 書き込み ---
   144                                      ; アドレス [DS:SI] にテスト値を書き込む
   145 0000003B C60455                      mov byte [si], 0x55
   146                                  
   147                                      ; --- 読み出し ---
   148                                      ; アドレス [DS:SI] から値を読み出す
   149 0000003E 8A04                        mov al, byte [si]
   150                                  
   151                                      ; --- 比較 ---
   152 00000040 3C55                        cmp al, 0x55
   153 00000042 7516                        jne error_found         ; 一致しない場合はエラー処理へ
   154                                  
   155                                  next_addr:
   156                                      ; --- アドレス更新 ---
   157 00000044 46                          INC SI                  ; アドレスをインクリメント
   158 00000045 83FE00                      CMP SI, 0000H           ; 0000Hに戻ったかチェック (FFFFHの次は0000H)
   159 00000048 75F1                        JNZ main_loop           ; 0でなければループ継続
   160                                  
   161                                      ; --- 終了処理 (全チェック完了) ---
   162                                      ; シリアル送信部
   163 0000004A BA6112                      mov dx, SCU_SST
   164                                  .wait_tx3:
   165 0000004D EC                          in al, dx
   166 0000004E A801                        test al, 00000001b  ; TX Ready?
   167 00000050 74FB                        jz .wait_tx3
   168                                      
   169 00000052 BA6012                      mov dx, SCU_DATA
   170 00000055 B023                        mov al, '#'
   171 00000057 EE                          out dx, al
   172                                  
   173                                      ;無限ループで停止
   174 00000058 EBFE                        jmp $
   175                                  
   176                                  ; -------------------------------------------------------------
   177                                  ; エラー処理ルーチン: アドレス(SI)を16進数で出力
   178                                  ; 注意: CALLは使えないため、処理後は NEXT_ADDR へジャンプして戻る
   179                                  ; -------------------------------------------------------------
   180                                  error_found:
   181                                      ; 現在のSI (エラーアドレス) を保持して表示処理を行う
   182                                      ; ループ用にBPを使用 (スタック操作ではない汎用レジスタとして利用)
   183                                      ; 16bitアドレスなので4桁のHEXを表示する
   184                                  
   185 0000005A B90400                      MOV CX, 4               ; 4桁分ループ (4 nibbles)
   186                                      ; データ操作用には BP を使用 (DXを温存するため)
   187                                      ; スタックを使わないのでBPは汎用レジスタとして利用可能
   188 0000005D 89F5                        MOV BP, SI              ; エラーアドレスをBPにコピー
   189                                  
   190                                  PRINT_HEX_LOOP:
   191                                      ; 上位4ビットを取り出すために左ローテート
   192 0000005F D1C5                        ROL BP, 1
   193 00000061 D1C5                        ROL BP, 1
   194 00000063 D1C5                        ROL BP, 1
   195 00000065 D1C5                        ROL BP, 1
   196                                      
   197 00000067 89E8                        MOV AX, BP              ; AXにコピー
   198 00000069 83E00F                      AND AX, 000FH           ; 下位4ビットのみ抽出 (0-F)
   199                                  
   200                                      ; HEX ASCII変換
   201 0000006C 3C0A                        CMP AL, 0AH
   202 0000006E 7202                        JB  IS_DIGIT
   203 00000070 0407                        ADD AL, 07H             ; 'A'-'F' の場合の調整
   204                                  IS_DIGIT:
   205 00000072 0430                        ADD AL, 30H             ; '0' のASCIIコードを足す
   206                                  
   207                                      ; --- 文字送信 (SCU) ---
   208                                      ; レジスタ退避ができないため、他のレジスタを壊さないよう注意
   209                                      ; ここでは AH を作業用、AL を送信データとして使用
   210                                      
   211 00000074 88C3                        MOV BL, AL              ; 送信文字をBLに一時退避
   212                                  
   213                                      ; シリアル送信部
   214 00000076 BA6112                      mov dx, SCU_SST
   215                                  .wait_tx:
   216 00000079 EC                          in al, dx
   217 0000007A A801                        test al, 00000001b  ; TX Ready?
   218 0000007C 74FB                        jz .wait_tx
   219                                      
   220 0000007E BA6012                      mov dx, SCU_DATA
   221 00000081 88D8                        mov al, bl
   222 00000083 EE                          out dx, al
   223                                  
   224 00000084 E2D9                        LOOP PRINT_HEX_LOOP     ; CXを減らしてループ
   225                                  
   226                                      ; アドレスの後にスペースなどを送る
   227                                      ; シリアル送信部
   228 00000086 BA6112                      mov dx, SCU_SST
   229                                  .wait_tx2:
   230 00000089 EC                          in al, dx
   231 0000008A A801                        test al, 00000001b  ; TX Ready?
   232 0000008C 74FB                        jz .wait_tx2
   233                                      
   234 0000008E BA6012                      mov dx, SCU_DATA
   235 00000091 B020                        mov al, ' '
   236 00000093 EE                          out dx, al
   237                                  
   238                                      ; --- テスト継続 ---
   239 00000094 EBAE                        JMP next_addr
   240                                  
   241                                  ; -----------------------------------------------------------------
   242                                  ; Padding (空白埋め)
   243                                  ; コードの終わりから、リセットベクタの手前までを0xFFで埋める
   244                                  ; -----------------------------------------------------------------
   245 00000096 FF<rep 1FF5Ah>              times ROM_TOTAL - 16 - ($ - $$) db 0xFF
   246                                  
   247                                  ; -----------------------------------------------------------------
   248                                  ; Reset Vector (Physical Address FFFF0h)
   249                                  ; CPUは電源ON時、ここ(ROMの末尾16バイト地点)を実行する
   250                                  ; -----------------------------------------------------------------
   251                                  reset_vector:
   252                                      ; ROMの先頭(start)へジャンプする
   253                                      ; 1MBit ROMの場合、物理アドレスは E0000h～FFFFFh にマップされる想定
   254                                      ; なので、セグメント E000h, オフセット 0000h へ飛べばよい
   255                                      
   256 0001FFF0 EA000000E0                  jmp 0xE000:0000   ; Far Jump to start of 128KB ROM
   257                                  
   258                                      ; ファイルサイズがぴったりROMサイズになるよう調整
   259 0001FFF5 FF<rep Bh>                  times 16 - ($ - reset_vector) db 0xFF
